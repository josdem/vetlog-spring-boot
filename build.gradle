buildscript {
  repositories {
    mavenCentral()
  }
  dependencies {
    classpath("org.flywaydb:flyway-mysql:10.17.3")
  }
}

plugins {
  id 'org.springframework.boot' version '3.3.5'
  id 'io.spring.dependency-management' version '1.1.6'
  id 'org.flywaydb.flyway' version '10.21.0'
  id 'org.sonarqube' version '5.1.0.4882'
  id 'jacoco'
  id 'java'
  id 'com.diffplug.spotless' version '6.25.0'
}

ext {
  gcpVersion = "5.8.0"
  retrofitVersion = '2.11.0'
  mockitoCoreVersion = '5.14.2'
}

group = 'com.josdem.vetlog'
version = '2.0.3.8'

configurations {
  compileOnly {
    extendsFrom annotationProcessor
  }
}


repositories {
  mavenCentral()
}

flyway {
  url = 'jdbc:mysql://localhost:3306/vetlog'
}

sonar {
  properties {
    property "sonar.projectKey", "josdem_vetlog-spring-boot"
    property "sonar.organization", "josdem-io"
    property "sonar.host.url", "https://sonarcloud.io"
  }
}

spotless {
  java {
    target fileTree('.') {
      include '**/*.java'
      exclude '**/build/**', '**/build-*/**'
    }
    toggleOffOn()
    palantirJavaFormat()
    removeUnusedImports()
    trimTrailingWhitespace()
    endWithNewline()
  }
}

dependencies {
  implementation 'org.springframework.boot:spring-boot-starter-web'
  implementation 'org.springframework.boot:spring-boot-starter-aop'
  implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'
  implementation "com.google.cloud:spring-cloud-gcp-starter-storage"
  implementation "com.squareup.retrofit2:converter-gson:$retrofitVersion"
  implementation "com.squareup.retrofit2:converter-jackson:$retrofitVersion"
  implementation 'org.thymeleaf.extras:thymeleaf-extras-springsecurity6'
  implementation 'org.springframework.boot:spring-boot-starter-security'
  implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
  implementation 'org.springframework.boot:spring-boot-starter-actuator'
  implementation 'org.springframework.boot:spring-boot-starter-validation'
  implementation platform("com.google.cloud:spring-cloud-gcp-dependencies:$gcpVersion")
  compileOnly 'org.projectlombok:lombok'
  annotationProcessor 'org.projectlombok:lombok'
  runtimeOnly 'com.mysql:mysql-connector-j'
  implementation 'net.minidev:json-smart:2.4.9'
  implementation 'javax.xml.bind:jaxb-api:2.3.1'
  testImplementation "cglib:cglib-nodep:3.2.4"
  testCompileOnly 'org.projectlombok:lombok'
  testAnnotationProcessor 'org.projectlombok:lombok'
  testImplementation 'org.springframework.boot:spring-boot-starter-test'
  testImplementation 'org.springframework.security:spring-security-test'
  testImplementation 'io.projectreactor:reactor-test'
  testImplementation "org.mockito:mockito-core:$mockitoCoreVersion"
}

jacoco {
  toolVersion = "0.8.11"
}

jacocoTestReport {
  reports {
    xml.required = true
    html.required = true
  }
}

java {
  toolchain {
    languageVersion = JavaLanguageVersion.of(21)
  }
}

tasks.named('test') {
  useJUnitPlatform()
  systemProperties = System.properties
}

springBoot {
  buildInfo()
}

bootRun {
  systemProperties = System.properties
}

test.dependsOn("spotlessApply")
bootRun.dependsOn("flywayMigrate")
